#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use Getopt::Long 2.33 qw{ :config auto_version };
use Pod::Usage;
use Test::More 0.88;
use Test::Prereq::Meta;

our $VERSION = '0.000_008';

my %opt = (
    ignore_url	=> [],
);
my $dump;

GetOptions( \%opt,
    qw{
	meta_file|meta-file=s@
	name=s
	perl_version|perl-version=s
	prune=s@
    },
    help	=> sub { pod2usage( { -verbose => 2 } ) },
) or pod2usage( { -verbose => 0 } );

my $tpm = Test::Prereq::Meta->new(
    meta_file		=> $opt{meta_file},
    name		=> $opt{name},
    perl_version	=> $opt{perl_version},
    prune		=> $opt{prune},
);

$tpm->all_prereq_ok( @ARGV );

done_testing();

__END__

=head1 TITLE

test-prereq-meta - Test whether all required modules are listed in the distribution's prerequisites

=head1 SYNOPSIS

 test-prereq-meta
 test-prereq-meta fubar.pm
 test-prereq-meta 'L<Foobar|Foo/bar>'
 test-prereq-meta -help
 test-prereq-meta -version

=head1 OPTIONS

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --meta-file

 --meta-file MYMETA.yml

This option specifies the name of the file containing the distribution's
meta data. It can be specified more than once, in which case the first
file that is readable is used.

If unspecified, L<Test::Prereq::Meta|Test::Prereq::Meta>'s default is
used.

=head2 --name

 --name '%m in %f'

This option specifies the name of the generated tests.

If unspecified, L<Test::Prereq::Meta|Test::Prereq::Meta>'s default is
used.

=head2 --perl-version

This option specifies the version of Perl whose core modules are
exempted from being included in the prerequisites.

If unspecified, L<Test::Prereq::Meta|Test::Prereq::Meta>'s default is
used.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script tests a Perl distribution (or selected files in that
distribution) to ensure that all modules required are named as
requirements in the meta data for the distribution.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2021 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
